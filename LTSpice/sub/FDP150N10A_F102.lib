.SUBCKT FDP150N10A_F102 2 1 3
******************************************************************
**      Fairchild Discrete Modeling Group                       **
******************************************************************
**      Website         www.fairchildsemi.com\models            **
******************************************************************
**      (C) Copyright 2009 Fairchild Semiconductor Corporation  **
**                      All rights reserved                     **
**                                                              **
**                    FDP150N10A_F102 Spice model               **
**                    Revision RevA, 01 June 2011               **
******************************************************************
*Nom Temp 25 deg C
Dbody 7 5 DbodyMOD 
Dbreak 5 11 DbreakMOD 
Lgate 1 9 4.534e-9
Ldrain 2 5 1.44e-9
Lsource 3 7 2.731e-9
RLgate 1 9 45.34
RLdrain 2 5 14.4
RLsource 3 7 27.31
Rgate 9 6 1.68

* Shielded  Gate
D1 100 5 D_SG_cap
D2 100 101 D_SG_cap
R1 101 7 0.876
C1 6 101 85e-12
.MODEL D_SG_cap D (IS=1e-9 n=1 RS=4e-3 CJO=1.51e-9 M=0.535 t_abs=25)

It 7 17 1
Ebreak 11 7 17 7 109
Rbreak 17 7 RbreakMOD 1 
.MODEL RbreakMOD RES (TC1=0.68e-3 TC2=-0.42e-6)
.MODEL DbodyMOD D (IS=8e-12 n=1.075 RS=3e-3 TRS1=1.85e-3 TRS2=1e-6 
+ CJO=0.383e-9 M=0.39 TT=1e-9 XTI=2.75)
.MODEL DbreakMOD D (RS=8e-3 TRS1=1e-3 TRS2=1e-6 )
Rsource 7a 7 0.346e-3
Rdrain 5 16 RdrainMOD 10e-3
.MODEL RdrainMOD RES (TC1=7.5e-3 TC2=25e-6)
M_BSIM3 16 6 7a 7a Bsim3 W=2.74437085714286 L=1.15e-6 NRS=0 NRD=0
.MODEL Bsim3 NMOS (LEVEL=7 VERSION=3.1 MOBMOD=3 CAPMOD=2 paramchk=1 NQSMOD=0
*Process Parameters
+ TOX=1000e-10 ;Oxide thickness
+ XJ=0.62e-6 ;Channel depth
+ NCH=0.96e17 ;Channel concentration
*Channel Current
+ U0=700 VSAT=500000 DROUT=1.8
+ DELTA=0.25 PSCBE2=0 RSH=0.346e-3
*Threshold voltage
+ VTH0=2.75
*Sub-threshold characteristics
+ VOFF=-0.1 NFACTOR=1.4
*Junction diodes and Capacitance
+ LINT=0.175e-6 DLC=0.175e-6 
+ CGSO=122e-12 CGSL=0 CGDO=0.7e-12 CGDL=120e-12
+ CJ=0 CF=0 CKAPPA=0.75
* Temperature parameters 
+ KT1=-1.6 KT2=0 UA1=1.5e-9
+ NJ=10)
.ENDS   
